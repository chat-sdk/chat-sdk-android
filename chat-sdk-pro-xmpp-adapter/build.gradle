plugins {
    id 'com.android.library'

    id 'maven-publish'
    id 'signing'
    id 'kotlin-android'
}

android {

    compileSdkVersion Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion Integer.parseInt(project.MIN_SDK)

    }

    buildTypes {
        debug {
            minifyEnabled project.hasProperty('minifyDebug') && minifyDebug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../chat-sdk-proguard-rules.pro', 'proguard-rules.pro'
        }
        release {
            minifyEnabled project.hasProperty('minifyRelease') && minifyRelease
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../chat-sdk-proguard-rules.pro', 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
        lintConfig file('lint.xml')
    }
    namespace 'app.xmpp.adapter'

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':chat-sdk-core')
    implementation project(':chat-sdk-core-ui')
    implementation project(":sdk-guru-licensing")

    implementation "id.zelory:compressor:$compressorVersion"
//    implementation "com.google.android.gms:play-services-maps:$playMapsVersion"

    api "org.igniterealtime.smack:smack-android:$smackVersion"
    api "org.igniterealtime.smack:smack-android-extensions:$smackVersion"

    // Optional for XMPPTCPConnection
    api "org.igniterealtime.smack:smack-tcp:$smackVersion"

    // Optional for XMPP-IM (RFC 6121) support (Roster, Threaded Chats, â€¦)
    api "org.igniterealtime.smack:smack-im:$smackVersion"

    // Optional for XMPP extensions support
    api "org.igniterealtime.smack:smack-extensions:$smackVersion"
    api "org.igniterealtime.smack:smack-experimental:$smackVersion"
    api "org.igniterealtime.smack:smack-sasl-provided:$smackVersion"

}

configurations {
    all*.exclude group: 'xpp3', module: 'xpp3'
}

ext {
    libraryName = 'XMPPAdapterPro'
    artifact = 'pro-xmpp-adapter'
    libraryDescription = 'XMPP Adapter Pro Module'
}

if (project.hasProperty("mavenPublish") && mavenPublish) {
    apply from: '../bintray.gradle'
}

